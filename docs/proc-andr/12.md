# 12.可视化身体活动

在本章中，我们将介绍智能手表和可穿戴设备上的一些身体传感器，以及我们可以用来实时读取和使用这些传感器数据的技术。

## 身体传感器

许多不同种类的可穿戴设备中都有身体传感器，特别是为个人监测身体活动而设计的健身追踪器。这些设备通常连接到移动应用程序，帮助用户跟踪他们的进展。大多数 Android 智能手表都配有至少两种身体传感器，一个计步器或计步器，以及一个心率传感器。这表明，活动追踪器(也包括时钟功能)和适当的智能手表之间有一些重叠。一些 Android 智能手表(如 Polaris M600)甚至打算主要用作活动跟踪器。

在前两章中，我们学习了如何使用处理来创建动画手表面部，在此之前，我们详细介绍了如何使用 Android API 和 Ketai 库访问传感器数据。我们现在应该能够结合这些技术来创建手表表面，从手表上的身体传感器读取数据，并通过动态可视化将这些数据呈现给用户。

### 步进计数器

计步器是监测身体活动最常见的传感器。它从加速度计读取数据，以推断佩戴者的运动模式，特别是与行走或跑步相关的运动模式。步数是总体身体活动的一个替代指标，尽管准确性有限，因为它不能衡量不涉及行走或跑步或运动强度的其他形式的活动。

每天 10，000 步是一个通常被接受的足够水平的身体活动的目标，但围绕这个数字作为健身的普遍目标一直存在一些争议( [`https://www.ncbi.nlm.nih.gov/pubmed/14715035`](https://www.ncbi.nlm.nih.gov/pubmed/14715035)) )。

### 心率

心率是一个非常精确的体力消耗指标，智能手表上的心率监视器允许我们实时访问这些信息。光学心率监测器，如智能手表中的光学心率监测器，通常不如其他类型的监测器可靠。光学监视器在一个称为光电容积描记术的过程中测量心率，其中它们将光(通常来自 led)照射到皮肤上，并检测由于血流变化引起的光衍射差异。手表处理这些数据，生成脉搏读数，并显示给用户。

另一方面，心电图(ECG)传感器直接测量心脏活动的电信号，但它们需要将电极连接到身体的不同部位，遵循与二十世纪初第一台心电图仪相同的原理(图 [12-1](#Fig1) )。医疗 ECG 传感器可以使用多达 12 个电极，但运动型胸带依赖于放置在心脏附近的单个 ECG 传感器。然而，智能手表和其他可穿戴设备中的光学监视器已经发展到可以足够准确地全天连续监控心率，并且最大限度地减少了不便。

![A432415_1_En_12_Fig1_HTML.jpg](A432415_1_En_12_Fig1_HTML.jpg)

图 12-1。

An early electrocardiograph from 1911\. Note the arms and one leg of the patient immersed in buckets, which contain a saline solution to conduct the body’s current. Modern ECG sensors are still based on this three-point principle, but optical sensors rely on a completely different physical process to measure heart rate.

## 实时可视化身体活动

使用处理来实现显示步数或心率数据的表盘并不困难。我们可以像之前处理其他传感器一样检索传感器数据，比如加速度计和陀螺仪。我们需要做的就是创建一个传感器管理器，从中获取相应的传感器对象，并附加一个侦听器，该侦听器将返回硬件测量的实际值。

### 简单计步器

访问计步传感器不需要任何特殊许可。这个传感器持续运行，即使我们的手表表面没有访问它。一个特点是，它返回自手表上次启动以来的步数，并且只有在系统重新启动时才重置为零。因此，如果我们想显示自启动手表界面以来的步数，我们必须存储从监听器接收的第一步计数值，并从所有后续值中减去它，如清单 [12-1](#Par10) 所示。

```java
import android.content.Context;
import android.hardware.Sensor;
import android.hardware.SensorManager;
import android.hardware.SensorEvent;
import android.hardware.SensorEventListener;

Context context;
SensorManager manager;
Sensor sensor;
SensorListener listener;

int offset = -1;
int steps;

void setup() {
  fullScreen();
  frameRate(1);
  textFont(createFont("SansSerif", 28 * displayDensity));
  textAlign(CENTER, CENTER);
  initCounter();
}

void draw() {
  background(0);
  translate(0, wearInsets().bottom/2);
  text(steps + " steps", 0, 0, width, height);
}

void initCounter() {
  Context context = getContext();
  manager = (SensorManager)context.getSystemService(Context.SENSOR_SERVICE);
  sensor = manager.getDefaultSensor(Sensor.TYPE_STEP_COUNTER);
  listener = new SensorListener();
  manager.registerListener(listener, sensor,
                           SensorManager.SENSOR_DELAY_NORMAL);
}

class SensorListener implements SensorEventListener {
  public void onSensorChanged(SensorEvent event) {
    if (offset == -1) offset = (int)event.values[0];
    steps = (int)event.values[0] - offset;
  }
  public void onAccuracyChanged(Sensor sensor, int accuracy) { }
}

Listing 12-1.Displaying the Step Count

```

这里，我们使用初始化为-1 的`offset`变量来存储初始步数。此外，如果我们计划创建一个跟踪每日步数的表盘，我们应该实现自己的“夜间重置”，因为 Android 不会在一天结束时自动重置步数。

### 访问心率传感器

心率需要`BODY_SENSORS`权限，由于数据的个人性质，该权限被归类为关键或危险权限。与地理定位一样，通过 PDE 中的 Android 权限选择器选择权限是不够的(图[12-2](#Fig2))；我们必须在代码中用`requestPermission()`函数手动请求权限，提供权限请求的结果所调用的函数的名称。见清单 [12-2](#Par13) 。

![A432415_1_En_12_Fig2_HTML.jpg](A432415_1_En_12_Fig2_HTML.jpg)

图 12-2。

The BODY_SENSORS permission in the selector

```java
import android.content.Context;
import android.hardware.Sensor;
import android.hardware.SensorManager;
import android.hardware.SensorEvent;
import android.hardware.SensorEventListener;

Context context;
SensorManager manager;
Sensor sensor;
SensorListener listener;

int bpm;

void setup() {
  fullScreen();
  frameRate(1);
  textFont(createFont("SansSerif", 28 * displayDensity));
  textAlign(CENTER, CENTER);
  requestPermission("android.permission.BODY_SENSORS", "initMonitor");
}

void draw() {
  background(0);
  translate(0, wearInsets().bottom/2);
  text(bpm + " beats/min", 0, 0, width, height);
}

void initMonitor(boolean granted) {
  if (granted) {
    Context context = getContext();
    manager = (SensorManager)context.
              getSystemService(Context.SENSOR_SERVICE);
    sensor = manager.getDefaultSensor(Sensor.TYPE_HEART_RATE);
    listener = new SensorListener();
    manager.registerListener(listener, sensor,
                             SensorManager.SENSOR_DELAY_NORMAL);
  }
}

class SensorListener implements SensorEventListener {
  public void onSensorChanged(SensorEvent event) {
    bpm = int(event.values[0]);
  }
  public void onAccuracyChanged(Sensor sensor, int accuracy) { }
}

Listing 12-2.Displaying the Heart Rate

```

当我们第一次打开表盘时，我们应该会看到一个类似于图 [12-3](#Fig3) 的对话框，询问允许或拒绝访问身体传感器数据。

![A432415_1_En_12_Fig3_HTML.jpg](A432415_1_En_12_Fig3_HTML.jpg)

图 12-3。

Permission request when launching the watch face

### 可视化步数数据

可视化活动数据的一种简单方法是使用放射状表示法，描绘朝着设定目标的进展。我们已经使用了`arc()`函数来显示经过的时间，并且我们可以很容易地修改它来显示朝着期望的计数值的进展；例如，100，如清单 [12-3](#Par16) 所示。

```java
...
void setup() {
  frameRate(1);
  strokeCap(ROUND);
  stroke(255);
  noFill();
  textFont(createFont("SansSerif", 18 * displayDensity));
  textAlign(CENTER, CENTER);
  initCounter();
}

void draw() {
  background(0);
  translate(0, wearInsets().bottom/2);
  if (wearAmbient()) strokeWeight(1);
  else strokeWeight(10);
  float angle = map(min(steps, 100), 0, 100, 0, TWO_PI);
  arc(width/2, height/2, width/2, width/2,
      PI + HALF_PI, PI + HALF_PI + angle);
  if (steps == 0) text("0 steps", 0, 0, width, height);
}
...

Listing 12-3.Radial Step-count Visualization

```

草图的其余部分与清单 [12-1](#Par10) 相同。接下来，我们可以添加一个乘数来显示用户已经达到目标的次数，方法是简单地将步数除以目标，在本例中为 100，并将该值作为文本绘制在屏幕中央。更新后的`draw()`功能如清单 [12-4](#Par18) 所示，这款新表盘的输出如图 [12-4](#Fig4) 所示。

![A432415_1_En_12_Fig4_HTML.jpg](A432415_1_En_12_Fig4_HTML.jpg)

图 12-4。

Step-count watch face, with multiplier counter

```java
void draw() {
  background(0);
  translate(0, wearInsets().bottom/2);
  if (wearAmbient()) strokeWeight(1);
  else strokeWeight(10);
  int mult = int(steps / 100);
  float angle = map(steps - mult * 100, 0, 100, 0, TWO_PI);
  noFill();
  arc(width/2, height/2, width/2, width/2,
      PI + HALF_PI, PI + HALF_PI + angle);
  fill(255);
  if (0 < steps) {
    text("x" + (mult + 1), 0, 0, width, height);
  } else {
    text("0 steps", 0, 0, width, height);
  }
}
Listing 12-4.Adding a Multiplier

```

### 跳动的心脏

对于心率数据的可视化表示，我们可以依赖一个非常直接的翻译:一颗跳动的心脏，或者为了简单起见，一个跳动的圆圈。我们已经知道如何从传感器获得每分钟心跳数的值；问题是基于这个速率来制作这个圆圈的动画，以便它足够准确地传达心跳的强度和节奏。实际的心电信号如图 [12-5](#Fig5) 所示，我们看到单次心跳的电信号有多个波峰和波谷。该信号描绘了所谓的窦性节律( [`https://en.wikipedia.org/wiki/Sinus_rhythm`](https://en.wikipedia.org/wiki/Sinus_rhythm) )，其中大的峰值对应于心室的收缩( [`https://en.wikipedia.org/wiki/Heart#Blood_flow`](https://en.wikipedia.org/wiki/Heart#Blood_flow) )。

![A432415_1_En_12_Fig5_HTML.jpg](A432415_1_En_12_Fig5_HTML.jpg)

图 12-5。

An ECG signal

我们可以用一条“脉冲”曲线来近似这种模式，该曲线呈现快速的初始增加，随后衰减，直到下一次搏动(图 [12-6](#Fig6) )。生成该脉冲曲线的简单数学函数如下:

![A432415_1_En_12_Fig6_HTML.jpg](A432415_1_En_12_Fig6_HTML.jpg)

图 12-6。

An impulse function, generated with Graph Toy, by Inigo Quilez ( [`http://www.iquilezles.org/apps/graphtoy/index.html`](http://www.iquilezles.org/apps/graphtoy/index.html) )

```java
float impulse(float k, float t) {
  float h = k * t;
  return h * exp(1.0 - h);
}

```

在这个公式中，常数 k 决定了脉冲多快达到峰值(峰值的位置正好是`t =1/k`)。通过检查图 [12-5](#Fig5) 中的 ECG 信号，我们可以得出结论，心跳的第一个峰值出现在整个心跳持续时间的 25%左右。例如，如果我们的心脏以每分钟 80 次(bpm)的速度跳动，那么单次跳动将持续 60，000/80 = 750 毫秒，其第一个峰值应该出现在大约 0.25 × 750 = 187.5 毫秒。由此，我们可以计算出`k`，因为 t = 187.5 = 1/k，在这种情况下给出`k`∞0.0053。一般来说，对于任何测得的 bpm 值，常数`k`将等于 BPM/(0.25×60000)。

我们可以使用这个脉冲公式来控制任何形状的动画。当然，这个公式并不精确，它的最大值不一定与心室收缩的精确时刻重合，但作为一个近似值应该足以传达跳动的步伐。清单 [12-5](#Par24) 扩展了我们之前清单 [12-2](#Par13) 中的心率表盘，加入了一个半径遵循脉冲函数的椭圆。和以前一样，我们必须通过 PDE 添加`BODY_SENSORS`权限，一旦手表界面在设备上或模拟器中启动，就授予这个权限。

```java
import android.content.Context;
import android.hardware.Sensor;
import android.hardware.SensorManager;
import android.hardware.SensorEvent;
import android.hardware.SensorEventListener;

Context context;
SensorManager manager;
Sensor sensor;
SensorListener listener;

int bpm;

void setup() {
  fullScreen();
  noStroke();
  textFont(createFont("SansSerif", 28 * displayDensity));
  textAlign(CENTER, CENTER);
  requestPermission("android.permission.BODY_SENSORS", "initMonitor");
}

void draw() {
  background(0);
  translate(0, wearInsets().bottom/2);
  if (wearAmbient()) {
    fill(255);
    text(bpm + " bpm", 0, 0, width, height);
  } else {
    int duration = 750;
    if (0 < bpm) duration = 60000 / bpm;
    float x = millis() % duration;
    float k = 1/(0.25 * duration);
    float a = impulse(k, x);
    float r = map(a, 0, 1, 0.75, 0.9) * width;
    translate(width/2, height/2);
    fill(247, 47, 47);
    ellipse(0, 0, r, r);
  }
}

float impulse(float k, float x) {
  float h = k * x;
  return h * exp(1.0 - h);
}

void initMonitor(boolean granted) {
  if (!granted) return;
  Context context = getContext();
  manager = (SensorManager)context.getSystemService(Context.SENSOR_SERVICE);
  sensor = manager.getDefaultSensor(Sensor.TYPE_HEART_RATE);
  listener = new SensorListener();
  manager.registerListener(listener, sensor,
                           SensorManager.SENSOR_DELAY_NORMAL);
}

class SensorListener implements SensorEventListener {
  public void onSensorChanged(SensorEvent event) {
    bpm = int(event.values[0]);
  }
  public void onAccuracyChanged(Sensor sensor, int accuracy) { }
}

Listing 12-5.Creating a Heart-beat Animation

```

仅当手表处于交互模式时，才会绘制椭圆。我们计算当前 bpm 下单次心跳的持续时间，并将其存储在`duration variable`中，我们在评估脉冲函数所需的所有其他参数中使用它。由于脉冲范围在 0 和 1 之间，我们将其映射到(0.75，0.9)区间，因此椭圆的大小在收缩和膨胀状态之间变化，既不太小也不太大。我们可以调整这些参数，直到我们得到视觉上满意的结果。

### 传感器调试

测试使用传感器的表盘可能会很困难，因为我们可能需要四处走动，从计步器或心率传感器获取足够的数据，以确保我们在代码中评估不同的实例。由于处理允许我们轻松地在模式之间来回切换，并且绝大多数处理 API 在 Java 和 Android 模式之间保持不变，因此我们可以使用 Java 模式来测试不依赖于实际传感器的代码部分，尤其是渲染代码。

然而，我们仍然经常需要检查实际的传感器数据，以确定是否有问题，要么是我们对数据的假设，要么是我们在代码中处理数据的方式。我们可以这样做的一个方法是，将传感器的值记录到一个文本文件中，然后从手表中取出这个文件，寻找任何感兴趣的模式。清单 [12-6](#Par28) 举例说明了这种保存心率传感器数据的方法。由于我们将数据写入外部存储器中的一个文件，我们需要将`WRITE_EXTERNAL_STORAGE`权限添加到草图中，并在草图代码中添加相应的请求，因为这也是一个危险的权限。

```java
import android.content.Context;
import android.hardware.Sensor;
import android.hardware.SensorManager;
import android.hardware.SensorEvent;
import android.hardware.SensorEventListener;
import android.os.Environment;

Context context;
SensorManager manager;
Sensor sensor;
SensorListener listener;

int bpm;
String[] data = { "time,rate" };

void setup() {
  fullScreen();
  frameRate(1);
  textFont(createFont("SansSerif", 28 * displayDensity));
  textAlign(CENTER, CENTER);
  requestPermission("android.permission.BODY_SENSORS", "initMonitor");
  requestPermission("android.permission.WRITE_EXTERNAL_STORAGE");
}

void draw() {
  background(0);
  translate(0, wearInsets().bottom/2);
  text(bpm + " beats/min", 0, 0, width, height);
}

void mousePressed() {
  background(200, 40, 40);
  File sd = Environment.getExternalStorageDirectory();
  String path = sd.getAbsolutePath();
  File directory = new File(path, "out");
  File file = new File(directory, "sensor-data.csv");
  saveStrings(file, data);
}

void initMonitor(boolean granted) {
  if (granted) {
    Context context = getContext();
    manager = (SensorManager)
              context.getSystemService(Context.SENSOR_SERVICE);
    sensor = manager.getDefaultSensor(Sensor.TYPE_HEART_RATE);
    listener = new SensorListener();
    manager.registerListener(listener, sensor,
                             SensorManager.SENSOR_DELAY_NORMAL);
  }
}

class SensorListener implements SensorEventListener {
  public void onSensorChanged(SensorEvent event) {
    bpm = int(event.values[0]);
    data = (String[]) append(data, millis() + "," + bpm);
  }
  public void onAccuracyChanged(Sensor sensor, int accuracy) { }
}

Listing 12-6.Saving Sensor Data to a File

```

首先，注意对`WRITE_EXTERNAL_STORAGE`权限的请求是如何不包括在权限被授予(或拒绝)后要调用的函数的。这是因为写入外部存储器不需要额外的初始化。

在这个草图中，每当我们接收到一个新的传感器值，我们就把它和以毫秒为单位的时间一起添加到字符串数组`data`中。只有在触摸屏幕时，我们才使用 saveStrings()函数将该数组作为 CSV(逗号分隔值)文件保存到设备的外部存储器中。手表中的外部存储在其内部存储中进行模拟，因为智能手表不包括 SD 卡。为了将文件下载到开发计算机，我们可以从终端运行以下命令:

```java
adb -s 127.0.0.1:4444 pull /storage/emulated/0/out/sensor-data.csv

```

一旦我们下载了数据文件，我们可以在文本编辑器或电子表格软件中阅读它。我们也可以在一个处理草图中使用它作为输入，就像清单 [12-7](#Par33) 中的那样，在那里我们用`loadTable()`函数( [`https://processing.org/reference/loadTable_.html`](https://processing.org/reference/loadTable_.html) )读取 CSV 文件。这个函数返回一个`Table`对象，包含所有组织成行和列的数据。在这种情况下，我们简单地绘制一个带有`LINE_STRIP`形状的线图，连接每个连续行中的值。该草图的典型结果如图 [12-7](#Fig7) 所示。

![A432415_1_En_12_Fig7_HTML.jpg](A432415_1_En_12_Fig7_HTML.jpg)

图 12-7。

Line plot of heart-rate data

```java
size(700, 200, P2D);
Table table = loadTable("sensor-data.csv", "header");
background(90);
stroke(247, 47, 47);
strokeWeight(4);
beginShape(LINE_STRIP);
for (int i = 0; i < table.getRowCount(); i++) {
  TableRow row = table.getRow(i);
  int r = row.getInt("rate");
  float x = map(i, 0, table.getRowCount() - 1, 0, width);
  float y = map(r, 0, 100, height, 0);
  vertex(x, y);
}
endShape();
Listing 12-7.Plotting Sensor Data in Processing

```

在这两个示例中，我们的目标是记录传感器数据，以便进行后续分析来识别数据中的任何问题。另一个不同但相关的目的是以更可控的方式调试我们的数据处理代码。我们可以通过生成类似传感器输出的“合成”数据来做到这一点。例如，清单 [12-8](#Par35) 显示了前面的心率示例，修改后可以打印连续运行的线程中生成的随机 bpm 值。

```java
int bpm;

void setup() {
  fullScreen();
  frameRate(1);
  textFont(createFont("SansSerif", 28 * displayDensity));
  textAlign(CENTER, CENTER);
  thread("generateData");
}

void draw() {
  background(0);
  translate(0, wearInsets().bottom/2);
  text(bpm + " beats/min", 0, 0, width, height);
}

void generateData() {
  while (true) {
    bpm = int(random(60, 100));
    delay(2000);
  }
}

Listing 12-8.Generating Synthetic Sensor Data

```

我们在第 6 章[中讨论了线程，包括我们如何使用它们来运行计算，否则会降低我们应用的帧率。这里，我们在一个新线程中在`setup()`的末尾启动`generateData()`函数。在该函数中，我们不断生成 60 到 100 之间的随机 bpm 值，每个连续值之间有两秒钟的延迟，这与真实心率传感器的行为非常接近，足以进行测试(延迟间隔也可以是随机的，以增加更多的可变性)。同样在第 6 章](06.html)[中，我们考虑了使用同步方法来防止不同的线程同时访问相同的数据。在这个最小的例子中，并发不是一个问题，但是我们需要在本章剩余部分将要考虑的最终项目中注意它。](06.html)

## 锻炼的同时种一棵树

到目前为止，我们的身体活动手表表面相对简单。我们是否可以通过提供一个视觉输出，不仅跟踪活动水平，还引入一些视觉变化，来使活动跟踪器更有趣(也许更有价值)？如果我们认为步数是一个从 0 开始增长的值，直到达到一个设定的目标，比如 10，000 步，是否有可能用它来驱动我们草图中某些有机元素的“增长”——例如，一株植物或一棵树？

用代码生成一棵看起来自然的树是一个问题，它可以将我们带到数学中令人着迷的想法，如自相似性和分形( [`https://en.wikipedia.org/wiki/Self-similarity`](https://en.wikipedia.org/wiki/Self-similarity) )。我们可以找到几种模拟树木生长的技术，其中一些如图 [12-8](#Fig8) 所示。

![A432415_1_En_12_Fig8_HTML.jpg](A432415_1_En_12_Fig8_HTML.jpg)

图 12-8。

Different algorithms for tree generation. Left: fractal recursion (by Daniel Shiffman, [`https://processing.org/examples/tree.html`](https://processing.org/examples/tree.html)) `)`. Center: branching tree (by Ryan Chao, [`https://www.openprocessing.org/sketch/186129`](https://www.openprocessing.org/sketch/186129) ). Right: particle system tree (by Asher Salomon, [`https://www.openprocessing.org/sketch/144159`](https://www.openprocessing.org/sketch/144159) )

最重要的是，我们需要一种算法，让我们的树随着步数的增加而增长。图 [12-8](#Fig8) 中提到的分形递归和粒子系统算法都可以随时间展开。尤其是后者，当它逐渐增长直至达到最大尺寸时，它比分形递归更有组织性。此外，它的代码可以在 OpenProcessing 上获得，所以我们可以用它作为我们项目的起点。

Note

OpenProcessing ( [`https://www.openprocessing.org/`](https://www.openprocessing.org/) )是一个在线加工草图库，其中大部分可以在浏览器中运行，可以根据知识共享协议进行修改和共享。

### 用粒子系统生成树

我们已经在第 6 章中使用了粒子系统来生成一个跟随图像亮度模式的动画。粒子系统可以在许多不同的场景中使用，以创建有机运动，就像这里的情况一样。对 Asher Salomon 的 OpenProcessing 草图做了一些修改，我们得到了清单 [12-9](#Par42) 中的代码，当在 Java 模式下运行时，它给出了如图 [12-9](#Fig9) 所示的输出。

![A432415_1_En_12_Fig9_HTML.jpg](A432415_1_En_12_Fig9_HTML.jpg)

图 12-9。

Output of the tree-generation algorithm

```java
ArrayList<Branch> branches = new ArrayList<Branch>();

void setup() {
  size(500, 500);
  noStroke();
  branches.add(new Branch());
  background(155, 211, 247);
}

void draw() {
  for (int i = 0; i < branches.size(); i++) {
    Branch branch = branches.get(i);
    branch.update();
    branch.display();
  }
}

class Branch {
  PVector position;
  PVector velocity;
  float diameter;

  Branch() {
    position = new PVector(width/2, height);
    velocity = new PVector(0, -1);
    diameter = width/15.0;
  }
  Branch(Branch parent) {
    position = parent.position.copy();
    velocity = parent.velocity.copy();
    diameter = parent.diameter / 1.4142;
    parent.diameter = diameter;
  }
  void update() {
    if (1 < diameter) {
      position.add(velocity);
      float opening = map(diameter, 1, width/15.0, 1, 0);
      float angle = random(PI - opening * HALF_PI,
                           TWO_PI + opening * HALF_PI);
      PVector shake = PVector.fromAngle(angle);
      shake.mult(0.1);
      velocity.add(shake);
      velocity.normalize();
      if (random(0, 1) < 0.04) branches.add(new Branch(this));
    }
  }
  void display() {
    if (1 < diameter) {
      fill(175, 108, 44, 50);
      ellipse(position.x, position.y, diameter, diameter);
    }
  }
}

Listing 12-9.Growing Tree

```

为了理解这段代码，我们应该看看`Branch`类是如何被用来表示一群移动的粒子的，当它们在屏幕上留下轨迹时，生成了树的分支。基本思想如下:每个粒子是一个有位置、速度和直径的小椭圆。最初，有一个单个粒子，放置在屏幕的底部，直径为 width/15(后面会详细介绍这个选择)，向上的速度为 1。有时，一个新的粒子会从一个现有的粒子中分支出来，所以我们最终会从一个主干中分出许多分支。为了正常工作，我们必须小心只在`setup()`中调用`background()`，这样粒子的轨迹不会在下一帧中被删除。

每次在`draw()`中调用一个粒子的`update()`方法，它的位置都会根据当前的速度更新，并且速度会被`shake`向量稍微颠簸一下。该向量的大小为 0.1，方向由`angle`变量决定。然而，在这个角度的计算方式中有一个重要的细节使得它不是完全随机的。这样做的原因是，我们希望避免树在早期摇摆到两侧，所以我们映射分支的直径，范围从开始的宽度/15 到结束的 1，到 0 和 1 之间的`opening`变量。如果树枝的直径接近 1，说明这棵树已经长好了，所以树枝不需要直，摇动矢量的角度可以在`PI - opening * HALF_PI`到`TWO_PI + opening * HALF_PI`之间的任何地方。如果`opening`正好为 1，则角度从整个圆范围内选择。另一方面，当`opening`开始接近 0°时，角度只能在`PI`和`TWO_PI`之间变化，代表圆的上半部分。这样，当树开始生长时，树枝被迫向上移动。

该算法的另一个关键方面是分支机制:每次粒子更新时，如果 0 和 1 之间的随机抽取小于 0.04，它会在当前位置创建一个新的分支。新分支只是另一个粒子对象，它从其父对象初始化，具有相同的位置和速度，但直径缩小了 1/ ![$$ \sqrt{2} $$](A432415_1_En_12_Chapter_IEq1.gif)。作为分支的结果，母粒子的直径也以同样的系数减小。

我们刚刚回顾了算法的主要元素。正如我们在代码中看到的，我们可以修改几个数字参数来调整树的外观。一个这样的选择是树枝的初始直径，这里设置为宽度/15，因为它给出了合理大小的树。此外，作为这个特定参数选择的结果，该算法将进行相当明确的迭代次数，以达到一个完全成长的树，在本例中大约是 300(我们将很快回到这个数字)。

### 结合步数数据

我们有一个树生成算法的工作版本，但它还没有绑定到步数传感器。检查这个传感器返回的值，我们意识到步骤不是一个接一个地被检测，而是我们得到一个以不规则的数量增加的数字。因此，我们可以计算当前和上一次`onSensorChanged()`事件之间的步数差异，并根据需要多次更新我们的粒子。让我们在清单 [12-10](#Par48) 中尝试这种方法。

```java
import android.content.Context;
import android.hardware.Sensor;
import android.hardware.SensorManager;
import android.hardware.SensorEvent;
import android.hardware.SensorEventListener;

Context context;
SensorManager manager;
Sensor sensor;
SensorListener listener;

int offset = -1;
int psteps, steps;
int stepInc = 0;

ArrayList<Branch> branches = new ArrayList<Branch>();
PGraphics canvas;

void setup() {
  fullScreen();
  noStroke();
  branches.add(new Branch());
  initCanvas();
  initCounter();
}

void draw() {
  background(0);
  if (wearInteractive()) growTree();
  image(canvas, 0, 0);
}

synchronized void growTree() {
  canvas.beginDraw();
  for (int s = 0; s < stepInc; s++) {
    for (int i = 0; i < branches.size(); i++) {
      Branch branch = branches.get(i);
      branch.update();
      branch.display();
    }
  }
  canvas.endDraw();
  stepInc = 0;
}

synchronized void updateSteps(int value) {
  if (offset == -1) offset = value;
  steps = value - offset;
  stepInc += steps - psteps;
  psteps = steps;
}

void initCanvas() {
  canvas = createGraphics(width, height);
  canvas.beginDraw();
  canvas.background(155, 211, 247);
  canvas.noStroke();
  canvas.endDraw();
}

class Branch {
  PVector position;
  PVector velocity;
  float diameter;

  Branch() {
    position = new PVector(width/2, height);
    velocity = new PVector(0, -1);
    diameter = width/15.0;
  }
  Branch(Branch parent) {
    position = parent.position.copy();
    velocity = parent.velocity.copy();
    diameter = parent.diameter / 1.4142;
    parent.diameter = diameter;
  }
  void update() {
    if (1 < diameter) {
      position.add(velocity);
      float opening = map(diameter, 1, width/15.0, 1, 0);
      float angle = random(PI - opening * HALF_PI,
                           TWO_PI + opening * HALF_PI);
      PVector shake = PVector.fromAngle(angle);
      shake.mult(0.1);
      velocity.add(shake);
      velocity.normalize();
      if (random(0, 1) < 0.04) branches.add(new Branch(this));
    }
  }
  void display() {
    if (1 < diameter) {
      canvas.fill(175, 108, 44, 50);
      canvas.ellipse(position.x, position.y, diameter, diameter);
    }
  }
}

void initCounter() {
  Context context = getContext();
  manager = (SensorManager)context.getSystemService(Context.SENSOR_SERVICE);
  sensor = manager.getDefaultSensor(Sensor.TYPE_STEP_COUNTER);
  listener = new SensorListener();
  manager.registerListener(listener, sensor,
                           SensorManager.SENSOR_DELAY_NORMAL);
}

class SensorListener implements SensorEventListener {
  synchronized void onSensorChanged(SensorEvent event) {
    updateSteps(int(event.values[0]));
  }
  void onAccuracyChanged(Sensor sensor, int accuracy) { }
}

Listing 12-10.Driving the Growth of the Tree with the Step Count

```

我们重用了清单 [12-9](#Par42) 中的大部分代码，并添加了标准的 Android 事件处理程序。但是有几个新的东西我们应该考虑。首先，我们将分支绘制到屏幕外的表面，我们已经在第 9 章中使用过了。原因如下:作为一个手表表面，我们的草图将不得不为环境模式呈现不同的输出。因为这需要擦除整个屏幕，我们将会丢失制作树的粒子轨迹。将它们绘制到一个单独的表面，我们可以随时显示，这是解决这个问题的一个简单方法。

我们还有两个同步函数，`growTree()`和`updateSteps()`。访问并修改`stepInc`变量，该变量包含自上次`onSensorChanged()`事件以来计算的步数。由于`growTree()`是从`draw()`调用的，因此是从 Processing 的动画线程调用的，而`updateSteps()`是从`onSensorChanged()`调用的，而`onSensorChanged()`又是从应用程序的主线程触发的，我们需要同步来避免这两个线程对`stepInc`的并发修改。

### 调整表盘

我们仍然有两个问题，我们的第一个版本的树看脸。一个是，由于一个步骤相当于一个分支更新周期，树将增长得太快，特别是如果我们希望只有当步骤数足够高时，比如 10，000，树才能达到最大大小。第二个问题是由计步传感器的性质引起的:`onSensorChanged()`可能以不规则的间隔被调用，在一个时刻有大的步进增加，而在另一个时刻有小的变化。特别是，如果增加非常大，`growTree()`可能需要很长时间运行，冻结表盘，因为它更新所有粒子的次数与自上次传感器改变事件以来计数的步数一样多。

为了解决第一个问题，让我们回忆一下我们之前的观察，在当前的参数选择下，该算法需要大约 300 次更新来完全生长该树。这意味着一个步骤应该只代表更新迭代的一部分。更准确地说，如果我们的目标是完成 10，000 步的树，那么一个单步对一次更新迭代的贡献将是 300/10，000。

其次，冻结问题的一个简单解决方案可以是删除`growTree()`函数中的第一个循环，并对每个粒子只运行一次更新，同时将`stepInc b` y 的值减少一。通过这种方式，我们每帧更新一次，因此表盘不会冻结，而是会一直增长直到`stepInc`为零。清单 [12-11](#Par54) 显示了实现这些调整所需的代码变化。

```java
...
int offset = -1;
int psteps, steps;
float stepInc = 0;
int stepGoal = 10000;
float stepScale = stepGoal / 300.0;
...
synchronized void growTree() {
  if (1 <= stepInc) {
    canvas.beginDraw();
    for (int i = 0; i < branches.size(); i++) {
      Branch branch = branches.get(i);
      branch.update();
      branch.display();
    }
    canvas.endDraw();
    stepInc--;
  }
}

synchronized void updateSteps(int value) {
  if (offset == -1) offset = value;
  steps = value - offset;
  stepInc += (steps - psteps) / stepScale;
  psteps = steps;
}
...

Listing 12-11.Controlling Growth Rate

```

### 让树开花

我们非常接近完成我们的手表脸！我们仍然需要一些改进:一个环境模式，它可以只是一个总步数和时间的文本绘图；当接近期望的计步目标时会有一个额外的动画——例如，众所周知的 10，000；以及达到目标后的重启。清单 [12-12](#Par56) 增加了这些改进，我们将在代码之后讨论。

```java
import android.content.Context;
import android.hardware.Sensor;
import android.hardware.SensorManager;
import android.hardware.SensorEvent;
import android.hardware.SensorEventListener;

Context context;
SensorManager manager;
Sensor sensor;
SensorListener listener;

int offset = -1;
int tsteps, psteps, steps, phour;
float stepInc = 0;
int stepGoal = 10000;
float stepScale = stepGoal / 300.0;

ArrayList<Branch> branches = new ArrayList<Branch>();
PGraphics canvas;
color bloomColor = color(230, 80, 120, 120);

void setup() {
  fullScreen();
  noStroke();
  textFont(createFont("SansSerif-Bold", 28 * displayDensity));
  branches.add(new Branch());
  initCanvas();
  initCounter();
}

void draw() {
  background(0);
  String str = hour() + ":" + nfs(minute(), 2) + ":" +
                              nfs(second(), 2) + "\n" +
               tsteps + " steps";
  if (wearInteractive()) {
    growTree();
    if (stepGoal <= steps) clearTree();
    image(canvas, 0, 0);
    textAlign(CENTER, BOTTOM);
    textSize(20 * displayDensity);
    fill(0, 80);
  } else {
    textAlign(CENTER, CENTER);
    textSize(28 * displayDensity);
    fill(200, 255);
    translate(0, wearInsets().bottom/2);
  }
  text(str, 0, 0, width, height);
}

synchronized void growTree() {
  if (1 <= stepInc) {
    canvas.beginDraw();
    for (int i = 0; i < branches.size(); i++) {
      Branch branch = branches.get(i);
      branch.update();
      branch.display();
      branch.bloom();
    }
    canvas.endDraw();
    stepInc--;
  }
}

synchronized void updateSteps(int value) {
  if (hour() < phour) tsteps = steps;
  if (offset == -1) offset = value;
  steps = value - offset;
  tsteps += steps - psteps;
  stepInc += (steps - psteps) / stepScale;
  psteps = steps;
  phour = hour();
}

synchronized void clearTree() {
  canvas.beginDraw();
  canvas.background(155, 211, 247);
  canvas.endDraw();
  branches.clear();
  branches.add(new Branch());
  offset = -1;
  steps = psteps = 0;
  bloomColor = color(random(255), random(255), random(255), 120);
}

void initCanvas() {
  canvas = createGraphics(width, height);
  canvas.beginDraw();
  canvas.background(155, 211, 247);
  canvas.noStroke();
  canvas.endDraw();
}

class Branch {
  PVector position;
  PVector velocity;
  float diameter;

  Branch() {
    position = new PVector(width/2, height);
    velocity = new PVector(0, -1);
    diameter = width/15.0;
  }
  Branch(Branch parent) {
    position = parent.position.copy();
    velocity = parent.velocity.copy();
    diameter = parent.diameter / 1.4142;
    parent.diameter = diameter;
  }
  void update() {
    if (1 < diameter) {
      position.add(velocity);
      float opening = map(diameter, 1, width/15.0, 1, 0);
      float angle = random(PI - opening * HALF_PI,
                           TWO_PI + opening * HALF_PI);
      PVector shake = PVector.fromAngle(angle);
      shake.mult(0.1);
      velocity.add(shake);
      velocity.normalize();
      if (random(0, 1) < 0.04) branches.add(new Branch(this));
    }
  }
  void display() {
    if (1 < diameter) {
      canvas.fill(175, 108, 44, 50);
      canvas.ellipse(position.x, position.y, diameter, diameter);
    }
  }
  void bloom() {
    if (0.85 * stepGoal < steps && random(0, 1) < 0.001) {
      float x = position.x + random(-10, +10);
      float y = position.y + random(-10, +10);
      float r = random(5, 20);
      canvas.fill(bloomColor);
      canvas.ellipse(x, y, r, r);
    }
  }
}

void initCounter() {
  Context context = getContext();
  manager = (SensorManager)context.getSystemService(Context.SENSOR_SERVICE);
  sensor = manager.getDefaultSensor(Sensor.TYPE_STEP_COUNTER);
  listener = new SensorListener();
  manager.registerListener(listener, sensor,
                           SensorManager.SENSOR_DELAY_NORMAL);
}

class SensorListener implements SensorEventListener {
  void onSensorChanged(SensorEvent event) {
    updateSteps(int(event.values[0]));
  }
  void onAccuracyChanged(Sensor sensor, int accuracy) { }
}

Listing 12-12.Adding Flowers, Time, and Step Count

```

环境模式的实现相当简单:在屏幕中央绘制一条文本消息。树开花也相对简单:一旦我们达到总步长目标的 85%，我们在每个分支粒子的当前位置附近放置一个椭圆，概率为 0.001。我们在最终的观看面中需要的另一个功能是在达到目标后重置，因此画布被清除，所有粒子被移除以从头开始一个新的树。这是在`clearTree()`函数中完成的，同步是因为它修改了`offset`、`pstep`和`step`变量，这些变量也在每个传感器改变事件中改变。此外，我们还添加了一个“午夜重置”，这样当小时回到零时(这种情况只会在午夜发生)，总步数`tstep`会被设置为当前步数，行`if (hour() < phour) tsteps = steps`在`updateSteps()`中。

图 [12-10](#Fig10) 显示了一棵树在不同生长阶段的一系列三个屏幕截图，从早期的步数跟踪到接近步数目标时开花。在图 [12-11](#Fig11) 中，我们可以看到表盘在实际手表上的环境和互动模式下的样子。

![A432415_1_En_12_Fig11_HTML.jpg](A432415_1_En_12_Fig11_HTML.jpg)

图 12-11。

Final version of the tree watch face, in ambient (left) and interactive (right) modes

![A432415_1_En_12_Fig10_HTML.jpg](A432415_1_En_12_Fig10_HTML.jpg)

图 12-10。

Three stages in the growth of the tree

正如我们前面所讨论的，测试和调试这个表面可能很有挑战性，因为我们需要走足够长的时间来观察树的生长变化。然而，我们可以很容易地生成类似于计步传感器输出的合成数据。清单 [12-13](#Par60) 展示了为了使用步数生成器而不是步数监听器，需要对代码进行的修改。这些变化实际上相当小，本质上是启动一个运行数据生成循环的线程。我们可以调整循环中的值，以增加或减少更新的频率，以及值的范围。这给了我们足够的灵活性来在不同的场景下评估我们的代码，这些场景很难用真实的步数数据来测试。

```java
...
void setup() {
  ...
  branches.add(new Branch());
  initCanvas();
  thread("generateData");
}
...
void generateData() {
  int total = 0;
  while (true) {
    total += int(random(10, 20));
    updateSteps(total);
    delay(500);
  }
}
Listing 12-13.Testing with Synthetic Data

```

开发过程的最后一步是将我们的手表表面上传到谷歌 Play 商店。运行 Wear 2.x 的 Android 设备可以安装独立的应用程序和观看面孔，而不需要“配套”的移动应用程序，正如在线开发者文档中关于打包和分发 Wear 应用程序的说明: [`https://developer.android.com/training/wearables/apps/packaging.html`](https://developer.android.com/training/wearables/apps/packaging.html) 。要从 PDE 获取手表表盘的签名包，我们可以遵循第 [3](03.html) 章中描述的常规应用的相同步骤。

首先，我们必须在草图文件夹内的清单文件中写一个包名。服务和应用程序的标签是可选的，但强烈建议使用，因为它将在整个手表 UI 中用于识别手表表面。下面提供了树监视面的完整清单文件:

```java
<?xml version="1.0" encoding="UTF-8"?>
<manifest xmlns:android="http://schemas.android.com/apk/res/android"
          android:versionCode="1" android:versionName="1.0"
          package="com.example.running_tree">
    <uses-sdk android:minSdkVersion="25" android:targetSdkVersion="25"/>
    <uses-feature android:name="android.hardware.type.watch"/>
    <application android:icon="@drawable/icon"
                 android:label="Running Tree" android:supportsRtl="true">
        <uses-library android:name="com.google.android.wearable"
                      android:required="false"/>
        <meta-data android:name="com.google.android.wearable.standalone"
                   android:value="true"/>
        <service android:label="Running Tree" android:name=".MainService"
                 android:permission="android.permission.BIND_WALLPAPER">
            <meta-data android:name="android.service.wallpaper"
                       android:resource="@xml/watch_face"/>
            <meta-data android:name=
                       "com.google.android.wearable.watchface.preview"
                       android:resource="@drawable/preview_rectangular"/>
            <meta-data android:name=
                       "com.google.android.wearable.watchface.preview_circular"
                       android:resource="@drawable/preview_circular"/>
            <meta-data android:name=
        "com.google.android.wearable.watchface.companionConfigurationAction"
                       android:value=
                      "com.catinean.simpleandroidwatchface.CONFIG_DIGITAL"/>
            <intent-filter>
                <action android:name=
                        "android.service.wallpaper.WallpaperService"/>
                <category android:name=
               "com.google.android.wearable.watchface.category.WATCH_FACE"/>
            </intent-filter>
        </service>
        <activity android:name="processing.android.PermissionRequestor"/>
    </application>
    <uses-permission android:name="android.permission.WAKE_LOCK"/>
</manifest>

```

最后，我们需要创建一个完整的图标集，包括所有 DPI 级别的六个应用程序图标(xxxhdpi 到 l DPI)以及圆形和矩形预览图标(图 [12-12](#Fig12) )。虽然目前大多数手表都提供 xhdpi 分辨率，但随着屏幕分辨率更高的设备的出现，这种情况可能会在不久的将来发生变化。

![A432415_1_En_12_Fig12_HTML.jpg](A432415_1_En_12_Fig12_HTML.jpg)

图 12-12。

Icon set needed to export the signed package

## 摘要

本章总结了可穿戴设备和表盘部分。虽然可穿戴开发本身是一个完整的领域，但我们能够应用在前面章节中学习的大多数技术来创建交互式图形和传感器数据的动态可视化，同时考虑到可穿戴设备在屏幕尺寸、分辨率和电池寿命方面的一些独特方面。
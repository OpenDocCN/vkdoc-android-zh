# 2.处理语言

如果您不熟悉处理语言，请阅读本章，了解 2D 形状的创建、几何变换和颜色的使用，以及如何处理触摸屏输入。本章以一个绘制草图的分步示例结束，我们将在第 [3](03.html) 章中使用它来学习如何将通过处理创建的应用程序导出和上传到谷歌 Play 商店。

## 艺术家和设计师的编程速写本

正如我们在前一章中所学的，处理语言，结合 PDE，使编程新手更容易开始创建交互式图形。这种语言被设计成最小化、简单易学，但又有足够的表现力来创建不同领域的基于代码的项目:生成艺术、数据可视化、声音艺术、电影、表演等等。它包括大约 200 个不同类别的函数——绘图、交互、排版等等——以及几个帮助处理表单、颜色和数据的类。

我们也可以将处理视为“编码素描本”，类似于我们用来快速起草和提炼想法的纸质素描本。这个类比的一个重要部分是，与纸质速写本一样，处理使我们能够尽快从代码中获得视觉反馈。下一节将描述处理过程中的基本结构，它使我们能够轻松地在屏幕上生成动画输出。

## 加工草图的设置/绘制结构

在大多数情况下，我们需要我们的处理草图连续运行，以便在屏幕上显示动画图形并跟踪用户输入。我们可以使用一个基本的代码结构来实现这样的交互式草图，其中我们首先在一个`setup()`函数中执行所有的初始化操作，然后在每次处理需要渲染一个新帧时运行一个`draw()`函数。

Note

本章中的所有代码示例都可以在 Java 或 Android 模式下运行，因为它们不依赖于特定于任何一种模式的处理语言的任何特性。此外，因为这本书的一个要求是对编程语言有一定程度的了解，所以我们不会讨论编程的基本概念(例如，条件、循环、注释)。

有了这个结构，我们可以创建一个每秒更新固定次数的动画，默认情况下是 60 次。在对`draw()`函数的每次调用中，我们不仅需要绘制构成合成的可视元素，还需要执行更新合成所需的所有计算。例如，在清单 [2-1](#Par7) 中，我们使用函数`line(x, 0, x, height)`绘制了一条从左向右水平穿过屏幕的垂直线。线的水平位置包含在变量`x`中，我们用`x = (x + 1) % width`在每一帧中更新它。在这一行代码中，我们将`x`加 1，然后计算屏幕宽度模的结果。由于“`a`对`b`取模”定义为`a`除以`b`的整数的余数(例如`9 % 4`是`1`)，所以结果只能是 0 到 b - 1 之间的数。因此，我们草图中的`x`不能小于 0，也不能大于 width - 1，这正是我们所需要的:`x`一次递增 1 个单位，到达右边缘后折回 0。该草图的输出如图 [2-1](#Fig1) 所示。

![A432415_1_En_2_Fig1_HTML.jpg](A432415_1_En_2_Fig1_HTML.jpg)

图 2-1。

Output of the animated line sketch

```java
int x = 0;

void setup() {
  size(600, 200);
  strokeWeight(2);
  stroke(255);  
}

void draw() {
  background(50);
  line(x, 0, x, height);
  x = (x + 1) % width;
}

Listing 2-1.A Sketch That Draws a Vertical Line Moving Horizontally Across the Screen

```

处理以每秒 60 帧的默认帧率调用`draw()`函数；然而，我们可以使用函数`frameRate(int fps)`来改变这个默认值。例如，如果我们在`setup(),`中添加`frameRate(1)`，草图将每秒绘制 1 帧。

有时，我们可能需要在几帧之后停止处理动画。我们可以分别使用`noLoop()`和`loop()`函数来停止和恢复动画。处理过程中有一个名为`looping`的布尔(逻辑)变量，根据草图是否运行动画循环，该变量为真或为假。我们可以在前面的代码中添加简单的击键检测功能来停止/恢复草图，这在清单 [2-2](#Par10) 中实现。

```java
int x = 0;

void setup() {
  size(600, 200);
  strokeWeight(2);
  stroke(255);  
}

void draw() {
  background(50);
  line(x, 0, x, height);
  x = (x + 1) % width;
}

void keyPressed() {
  if (looping) {
    noLoop();
  } else {
    loop();
  }
}

Listing 2-2.Pausing/Resuming the Animation Loop

```

除了这些交互式草图，我们还可以创建没有设置/绘制的静态草图，如果我们只想生成不需要更新的固定组合，这通常很有用。处理只运行这些草图中的代码一次。清单 [2-3](#Par12) 包含一个简单的静态草图，画出了图 [2-2](#Fig2) 中看到的白色圆圈。

![A432415_1_En_2_Fig2_HTML.jpg](A432415_1_En_2_Fig2_HTML.jpg)

图 2-2。

Output of the static sketch

```java
size(400, 400);
ellipse(200, 200, 150, 150);
Listing 2-3.Static Sketch Without setup() and draw() Functions

```

## 用代码绘图

上一节中的例子指出了基于代码的绘制中的一些重要概念。首先，我们需要指定想要在屏幕上绘制的元素的坐标；二是有函数，比如`line()`，可以让我们通过设置定义形状的适当数值，来绘制各种图元或形状；第三，我们可以设置这些形状的视觉“样式”(例如，笔画颜色和粗细)。

在处理过程中，我们可以绘制不同种类的形状(点、线、多边形)和特定的属性(笔画粗细和颜色、填充颜色等)。这些属性可以被认为是“样式参数”,一旦设置，就会影响之后绘制的所有内容。例如，清单 [2-4](#Par15) 中的每个圆圈有不同的填充颜色，但是如果我们注释掉第二个`fill()`调用，第一个和第二个圆圈将是红色的，因为在开始时设置的填充颜色会影响前两个`ellipse`调用。图 [2-3](#Fig3) 显示了该草图在这些情况下的输出。

![A432415_1_En_2_Fig3_HTML.jpg](A432415_1_En_2_Fig3_HTML.jpg)

图 2-3。

Effect of the calling order of style functions

```java
size(460, 200);
strokeWeight(5);
fill(255, 0, 0);
stroke(0, 255, 0);
ellipse(100, 100, 200, 200);
fill(255, 0, 200); // Comment this line out to make second circle red
stroke(0);
ellipse(250, 100, 100, 100);
fill(0, 200, 200);
ellipse(380, 100, 160, 160);
Listing 2-4.Setting Style Attributes

```

### 屏幕坐标

处理将其图形输出绘制到一个矩形像素网格中，沿水平方向(x 轴)从 0 到 width–1，沿垂直方向(y 轴)从 0 到 height–1，如图 [2-4](#Fig4) 所示。当在 Java 模式下运行代码时，该网格将包含在一个单独的输出窗口中，或者当使用 Android 模式时，该网格将位于设备屏幕的中心。

![A432415_1_En_2_Fig4_HTML.jpg](A432415_1_En_2_Fig4_HTML.jpg)

图 2-4。

Diagram of the screen’s pixels

在用 Processing 绘图时，我们需要记住 x 坐标是从左到右的，而 y 坐标是从上到下的。因此，像素(0，0)表示屏幕的左上角，像素(width-1，height-1)表示右下角。大多数 2D 绘图函数在处理过程中的参数指的是屏幕的像素坐标。例如，下面的示例代码将产生如图 [2-5](#Fig5) 所示的输出(为清晰起见，其中每个方块代表一个像素)。

![A432415_1_En_2_Fig5_HTML.jpg](A432415_1_En_2_Fig5_HTML.jpg)

图 2-5。

Pixels covered by a stroked rectangle in Processing

```java
stroke(200, 0, 0);
fill(100, 200, 100);
rect(2, 1, width – 1, height - 2);

```

我们应该根据屏幕大小的限制来调整我们用 Processing 绘制的形状的大小。一般情况下，参考屏幕尺寸时，建议使用`width`和`height`内部变量，而不是实际值；这样，我们可以重新调整尺寸，而不必修改绘图代码，就像清单 [2-5](#Par20) 中所做的那样。

```java
Size(800, 800);
stroke(0);
fill(180);
background(97);
line(width/2, 0, width/2, height);
line(0, height/2, width, height/2);  
rect(0, 0, 200, 200);
rect(width – 200, 0, 199, 200);
rect(width – 200, height – 200, 199, 199);
rect(0, height – 200, 199, 199);
rect(200, 200, width – 400, height – 400);
Listing 2-5.Using screen coordinates.

```

在这段代码中，一些矩形的宽度/高度不寻常，为 199。这就是为什么屏幕外边框上的线条是可见的，因为正如我们刚刚看到的，最后一行/列像素的 x 坐标是 height-1/width-1。草图的输出，所有外部笔画都落在边缘像素上，如图 [2-6](#Fig6) 所示，就像它出现在 Nexus 5X 手机上一样。您还会注意到，这个输出只占据了屏幕中心 800 × 800 的正方形，因为这是我们在代码中指定的大小。我们将在本章后面看到如何使用整个屏幕，在第 [4](04.html) 章我们将看到如何根据设备的分辨率缩放图形。

![A432415_1_En_2_Fig6_HTML.jpg](A432415_1_En_2_Fig6_HTML.jpg)

图 2-6。

Output of code Listing [2-5](#Par20), on a Nexus 5X phone

### 形式

我们可以通过处理生成的所有视觉形式都被绘制成二维或三维形状。通常，我们通过在`beginShape()`和`endShape()`函数中显式指定定义其边界的所有顶点来构造这些形状，如清单 [2-6](#Par23) 所示(其输出如图 [2-7](#Fig7) 所示)。

![A432415_1_En_2_Fig7_HTML.jpg](A432415_1_En_2_Fig7_HTML.jpg)

图 2-7。

Composition created with several shapes

```java
size(600, 300);  

beginShape(QUADS);
vertex(5, 250);
vertex(590, 250);
vertex(590, 290);
vertex(5, 290);
endShape();

beginShape();
vertex(30, 25);
vertex(90, 90);
vertex(210, 10);
vertex(160, 120);  
vertex(210, 270);  
vertex(110, 180);
vertex(10, 270);
vertex(60, 150);
endShape(CLOSE);  

beginShape(TRIANGLES);
vertex(50, 30);
vertex(90, 75);
vertex(110, 30);  
endShape();

ellipse(470, 80, 70, 70);

Listing 2-6.Using beginShape() and endShape()

```

尽管我们在第一个例子中没有使用`beginShape` / `endShape`，在第一个例子中，我们用内置函数`ellipse()`和`rect()`创建了原始形状，但这些只是对它们对应的`beginShape` / `endShape`调用的速记调用。事实上，我们可以使用`beginShape(int kind)`创建其他类型的图元形状，其中`kind`参数表示所需的图元。例如，在清单 [2-7](#Par25) 中，我们用一组三角形构造了一个正多边形，这些三角形从一个中心顶点呈扇形散开。

```java
size(300, 300);
int numTriangles = 10;
beginShape(TRIANGLE_FAN);
vertex(width/2, height/2);
for (int i = 0; i <= numTriangles; i++) {
  float a = map(i, 0, numTriangles, 0, TWO_PI);
  float x = width/2 + 100 * cos(a);
  float y = height/2 + 100 * sin(a);
  vertex(x, y);
}
endShape();
Listing 2-7.Creating a Triangle Fan

```

在这个例子中，我们使用一个`for`循环来迭代三角扇形的划分数。Processing 作为 Java 语言的扩展，继承了 Java 的所有控制结构，这是我们算法绘图所需要的。另外，注意函数`map()`的使用，它是处理 API 的一部分。这个函数非常有用，它允许我们将一个范围内的数值转换为另一个范围内的相应值。在这种情况下，索引`i`在 0 和`numTriangles`之间变化，我们要将其转换为 0 和 2π之间的角度。

![A432415_1_En_2_Fig8_HTML.jpg](A432415_1_En_2_Fig8_HTML.jpg)

图 2-8。

Outputs of the triangle fan example for different numbers of vertices

其他种类的原始形状有`TRIANGLE_STRIP`、`QUAD_STRIP`、`LINES`和`POINTS`，这些都在 Processing 的参考资料中有完整的记录。例如，当你需要创建一个矩形网格或一个挖空的圆时，`QUAD_STRIP`就变得很方便，就像我们在清单 [2-8](#Par28) 中所做的那样。

```java
size(300, 300);  
beginShape(QUAD_STRIP);
int numQuads = 10;
for (int i = 0; i <= numQuads; i++) {
  float a = map(i, 0, numQuads, 0, TWO_PI);
  float x0 = width/2 + 100 * cos(a);
  float y0 = height/2 + 100 * sin(a);
  float x1 = width/2 + 130 * cos(a);
  float y1 = height/2 + 130 * sin(a);    
  vertex(x0, y0);
  vertex(x1, y1);
}
endShape();
Listing 2-8.Creating a Quad Strip

```

通过调整`numQuads`变量的值，我们可以获得更多细节的几何图形，如图 [2-9](#Fig9) 所示。

![A432415_1_En_2_Fig9_HTML.jpg](A432415_1_En_2_Fig9_HTML.jpg)

图 2-9。

Quad strip example with different values for `numQuads`

然而，我们经常需要创建更复杂的形状，如曲线。尽管我们可以手动计算曲线上的顶点，但 Processing 提供了许多函数来精确地完成这项工作，特别是对于 Catmull-Rom 样条以及二次和三次贝塞尔曲线。例如，`bezierVertex()`函数允许我们在三次贝塞尔曲线上定义一个点。它需要曲线必须通过的锚点以及定义开始和结束方向的控制点。当开始一条贝塞尔曲线时，第一个锚点用一个常规的`vertex()`调用设置，如图 [2-10](#Fig10) 所示。

![A432415_1_En_2_Fig10_HTML.jpg](A432415_1_En_2_Fig10_HTML.jpg)

图 2-10。

Parameters of the bezierVertex() function

我们可以将几条贝塞尔曲线组合成一个形状，以便生成更复杂的图形，如清单 [2-9](#Par32) 所示。

```java
size(300, 300);
int numLobes = 4;
float radAnchor = 50;
float radControl = 150;
float centerX = width/2;
float centerY = height/2;
beginShape();
for (int i = 0; i < numLobes; i++) {
  float a = map(i, 0, numLobes, 0, TWO_PI);
  float a1 = map(i + 1, 0, numLobes, 0, TWO_PI);
  float cx0 = centerX + radControl * cos(a);
  float cy0 = centerY + radControl * sin(a);
  float cx1 = centerX + radControl * cos(a1);
  float cy1 = centerY + radControl * sin(a1);
  float x0 = centerX + radAnchor * cos(a);
  float y0 = centerY + radAnchor * sin(a);
  float x1 = centerX + radAnchor * cos(a1);
  float y1 = centerY + radAnchor * sin(a1);    
  vertex(x0, y0);
  bezierVertex(cx0, cy0, cx1, cy1, x1, y1);
}
endShape();
Listing 2-9.Creating Multi-lobed Shape with Bezier Curves

```

通过调整草图中的参数(叶瓣数、锚点半径、控制点半径)，我们可以获得一整个系列的形状，其中一些我们可以在图 [2-11](#Fig11) 中看到。

![A432415_1_En_2_Fig11_HTML.jpg](A432415_1_En_2_Fig11_HTML.jpg)

图 2-11。

Family of multi-lobed shapes created with Bezier curves

### 颜色

颜色是视觉设计的另一个重要组成部分，除了整个输出屏幕的背景颜色之外，Processing 还提供了许多功能来设置形状内部的颜色(填充颜色)和边缘的颜色(描边颜色)。

默认情况下，我们可以使用 0 到 255 之间的 RGB(红、绿、蓝)值来设置颜色，如清单 [2-10](#Par36) 中的代码及其在图 [2-12](#Fig12) 中的输出所示。

![A432415_1_En_2_Fig12_HTML.jpg](A432415_1_En_2_Fig12_HTML.jpg)

图 2-12。

Output of setting stroke and fill RGB colors

```java
size(600, 300);
strokeWeight(5);
fill(214, 87, 58);
stroke(53, 124, 115);
rect(10, 10, 180, 280);
stroke(115, 48, 128);
fill(252, 215, 51);
rect(210, 10, 180, 280);
stroke(224, 155, 73);
fill(17, 76, 131);
rect(410, 10, 180, 280);
Listing 2-10.Setting Fill and Stroke Colors Using RGB Values

```

即使我们可以使用 RGB 值创建几乎任何可以想象的颜色，也很难找到我们需要的颜色的正确数字组合。处理包括一个方便的颜色选择器工具来帮助我们交互地选择颜色，然后我们可以将它作为 RGB 值复制到我们的草图中。在 PDE 的 tools 菜单下，颜色选择器以及任何其他安装的工具都是可用的(图 [2-13](#Fig13) )。

![A432415_1_En_2_Fig13_HTML.jpg](A432415_1_En_2_Fig13_HTML.jpg)

图 2-13。

Color Selector tool

我们还可以在 HSB(色调、饱和度和亮度)空间中指定颜色。HSB 模式可以用`colorMode()`功能设置，这也允许我们设置每个组件的范围。在代码清单 [2-11](#Par39) 中，我们通过将圆周围的位置映射到色调来绘制色轮。

```java
size(300, 300);
colorMode(HSB, TWO_PI, 1, 1);
float centerX = width/2;
float centerY = height/2;
float maxRad = width/2;
strokeWeight(2);
stroke(0, 0, 1);
for (int i = 0; i < 6; i++) {
  float r0 = map(i, 0, 6, 0, 1);
  float r1 = map(i + 1, 0, 6, 0, 1);
  beginShape(QUADS);
  for (int j = 0; j <= 10; j++) {
    float a0 = map(j, 0, 10, 0, TWO_PI);
    float a1 = map(j + 1, 0, 10, 0, TWO_PI);  
    float x0 = centerX + maxRad * r0 * cos(a0);
    float y0 = centerY + maxRad * r0 * sin(a0);
    float x1 = centerX + maxRad * r1 * cos(a0);
    float y1 = centerY + maxRad * r1 * sin(a0);  
    float x2 = centerX + maxRad * r1 * cos(a1);
    float y2 = centerY + maxRad * r1 * sin(a1);
    float x3 = centerX + maxRad * r0 * cos(a1);
    float y3 = centerY + maxRad * r0 * sin(a1);
    fill(a0, r0, 1);
    vertex(x0, y0);
    vertex(x1, y1);
    vertex(x2, y2);
    vertex(x3, y3);
  }
  endShape();
}
Listing 2-11.Drawing a Color Wheel Using HSB Values

```

让我们在这个例子中注意一些重要的事情。首先，我们将色调的范围设置为 2π，以便使指数和颜色之间的转换更加直接。第二，我们用`QUADS`代替`QUAD_STRIP`。我们不能为一个条带中的每个四边形设置单独的颜色，因为它们都与前一个和下一个四边形共享一个公共边。相反，在`QUADS`形状中，每个四边形都是独立于其他四边形定义的，因此可以有不同的样式属性。我们最终的色轮如图 [2-14](#Fig14) 所示。

![A432415_1_En_2_Fig14_HTML.jpg](A432415_1_En_2_Fig14_HTML.jpg)

图 2-14。

Output of HSB color wheel example Note

我们还可以用十六进制(hex)格式指定颜色，这在 web 开发中非常常见；即`fill(#FF0000)`或`stroke(#FFFFFF)`。

### 几何变换

到目前为止，我们已经看到了如何构建形状和选择颜色。此外，我们需要能够通过应用平移、旋转和缩放变换来移动它们并改变它们的大小(图 [2-15](#Fig15) )。

![A432415_1_En_2_Fig15_HTML.jpg](A432415_1_En_2_Fig15_HTML.jpg)

图 2-15。

The three types of geometric transformations

虽然平移、旋转和缩放的概念很直观，但是很难预测几次连续变换的效果。在考虑变换时，想象一下变换只在应用后影响坐标会有所帮助。例如，如果我们沿 x 轴平移 20 个单位，沿 y 轴平移 30 个单位，那么随后将围绕点(20，30)进行旋转。相反，如果首先应用旋转，那么轴将被旋转，并且将沿着旋转的轴发生平移。因此，如果我们在这个变换链的末端绘制一个形状，它的最终位置可能会有所不同，这取决于它们的完成顺序(图 [2-16](#Fig16) )。

![A432415_1_En_2_Fig16_HTML.jpg](A432415_1_En_2_Fig16_HTML.jpg)

图 2-16。

Geometric transformations cannot be exchanged

我们可以用`pushMatrix()`函数保存当前的转换“状态”，用相应的`popMatrix()`函数恢复。我们必须总是成对地使用这两个函数。它们允许我们通过只对形状的特定子集设置变换来创建复杂的相对运动。例如，清单 [2-12](#Par45) 生成一个动画，显示一个椭圆和一个正方形围绕位于屏幕中心的一个较大的正方形旋转，而较小的正方形也围绕自己的中心旋转。图 [2-17](#Fig17) 显示了该动画的快照。

![A432415_1_En_2_Fig17_HTML.jpg](A432415_1_En_2_Fig17_HTML.jpg)

图 2-17。

Using pushMatrix() and popMatrix() to keep transformations separate

```java
float angle;

void setup() {
  size(400, 400);
  rectMode(CENTER);
  noStroke();
}

void draw() {
  background(170);
  translate(width/2, height/2);
  rotate(angle);
  rect(0, 0, 100, 100);
  pushMatrix();
  translate(150, 0);  
  rotate(2 * angle);
  scale(0.5);
  rect(0, 0, 100, 100);
  popMatrix();
  translate(0, 180);
  ellipse(0, 0, 30, 30);
  angle += 0.01;
}

Listing 2-12.Using pushMatrix() and popMatrix()

```

## 响应用户输入

键盘输入和触摸屏输入允许我们向草图中输入信息以控制其行为。由于用户可以在任何时候触摸屏幕或按键，而不一定是在处理绘制帧时，因此我们需要一种方法来检索这些信息，无论我们在草图中处于哪个绘制阶段。

Processing 提供了几个内置变量和函数来处理用户输入。变量`mouseX`和`mouseY`给出了在 Java 模式下鼠标的当前位置。这些变量在 Android 模式下仍然可用，尽管移动设备通常没有鼠标。在这种情况下，它们只是代表屏幕上第一个触摸点的位置(处理也支持多点触摸交互，这将在第 [5](05.html) 章中介绍)。`mouseX`和`mouseY`都由`mousePressed`补充，表示鼠标/触摸屏是否被按下。使用这些变量，我们可以用很少的代码创建一个简单的绘图草图，如清单 [2-13](#Par48) 中所示。它在手机上的输出将如图 [2-18](#Fig18) 所示。由于通过`size()`功能设置的宽度和高度小于屏幕分辨率，我们看到输出区域被我们无法绘制的浅色背景包围。然而，如果我们不使用`size(width, height)`初始化草图，而是使用`fullScreen()`功能，我们可以使用整个屏幕。这也有隐藏屏幕顶部的状态栏和底部的导航栏的优点。

![A432415_1_En_2_Fig18_HTML.jpg](A432415_1_En_2_Fig18_HTML.jpg)

图 2-18。

Drawing with ellipses

```java
void setup() {
  size(1000, 500);
  noStroke();
  fill(255, 100);
}

void draw() {
  if (mousePressed) {
    ellipse(mouseX, mouseY, 50, 50);
  }
}

Listing 2-13.A Free-hand Drawing Sketch Using Circles

```

当`mouseX/Y`存储鼠标/触摸的当前位置时，处理还提供变量`pmouseX`和`pmouseY`，它们存储先前的位置。通过连接`pmouseX/Y`坐标和`mouseX/Y`坐标，我们可以画出跟随指针移动的连续线条。清单 [2-14](#Par50) 展示了这种技术，它也使用了`fullScreen()`以便我们可以在整个屏幕表面上绘制，如图 [2-19](#Fig19) 所示。

![A432415_1_En_2_Fig19_HTML.jpg](A432415_1_En_2_Fig19_HTML.jpg)

图 2-19。

Output of our simple drawing sketch, in full-screen mode

```java
void setup() {
  fullScreen();
}

void draw() {
  if (mousePressed) {
    line(pmouseX, pmouseY, mouseX, mouseY);
  }
}

Listing 2-14.Another Free-hand Drawing

Sketch

```

## 创建一个画藤蔓的应用程序

我们最后一部分的目标是编写一个绘图应用程序，将算法形状融入手绘线条中。一种可能性是用类似于生长的植物、藤蔓、叶子和花的形状来增加由线提供的支架。我们之前学过的贝塞尔曲线可以用来生成看起来很自然的形状。用笔和纸画一些草图(图 [2-20](#Fig20) )也可以帮助我们尝试一些视觉创意。

![A432415_1_En_2_Fig20_HTML.jpg](A432415_1_En_2_Fig20_HTML.jpg)

图 2-20。

Sketches for the vine-drawing app

我们可以在之前的草图基础上继续。我们之前忽略的一件事是在形式和颜色上某种程度的“随机性”。处理中的`random(float a, float b)`函数允许我们在`a`和`b`之间选择随机数，然后我们可以在通过`bezierVertex()`函数构建的叶子/花朵形状中使用这些随机数。在清单 [2-15](#Par53) 中，我们应用`random`函数来引入我们形状的颜色和波瓣数量的变化，图 [2-21](#Fig21) 显示了三次单独运行的草图输出。

![A432415_1_En_2_Fig21_HTML.jpg](A432415_1_En_2_Fig21_HTML.jpg)

图 2-21。

Output of the flower/leaf sketch

```java
void setup() {
  size(600, 200);  
  frameRate(1);
}

void draw() {
  background(180);
  drawFlower(100, 100);
  drawFlower(300, 100);
  drawFlower(500, 100);  
}

void drawFlower(float posx, float posy) {
  pushMatrix();
  translate(posx, posy);
  fill(random(255), random(255), random(255), 200);
  beginShape();
  int n = int(random(4, 10));
  for (int i = 0; i < n; i++) {
    float a = map(i, 0, n, 0, TWO_PI);
    float a1 = map(i + 1, 0, n, 0, TWO_PI);    
    float r = random(10, 100);
    float x = r * cos(a);
    float y = r * sin(a);
    float x1 = r * cos(a1);
    float y1 = r * sin(a1);    
    vertex(0, 0);
    bezierVertex(x, y, x1, y1, 0, 0);    
  }
  endShape();
  popMatrix();
}

Listing 2-15.Generating Randomized Flowers/Leaves with Bezier Curves

```

除了花/叶，我们可以添加一些额外的元素；例如，一个生长中的螺旋分支结束于一个果实。螺旋有一个`x(t) = r(t) cos(a(t))`、`y(t) = r(t) sin(a(t))`形式的参数公式([、`https://www.khanacademy.org/tag/parametric-equations`、](https://www.khanacademy.org/tag/parametric-equations))，其中参数`t`从 0 到 1，控制曲线的增长。在调整了径向函数之后，我用`r(t) = 1/t`达到了令人满意的增长行为，所以我们可以从清单 [2-16](#Par55) 中的代码开始绘制一个单螺旋(图 [2-22](#Fig22) )。

![A432415_1_En_2_Fig22_HTML.jpg](A432415_1_En_2_Fig22_HTML.jpg)

图 2-22。

Output of our spiral parametric equation sketch

```java
size(300, 300);
noFill();
translate(width/2, height/2);
beginShape();
float maxt = 10;
float maxr = 150;
for (float t = 1; t < maxt; t += 0.1) {
  float r = maxr/t;
  float x = r  * cos(t);
  float y = r  * sin(t);
  vertex(x, y);
}
endShape();
Listing 2-16.Drawing a Spiral Using Parametric Equations

```

圈数由参数`t`的最大值控制，而最大半径决定螺旋向外延伸多少。这两个参数将在`random()`函数的帮助下给我们一些视觉上的变化，就像我们之前做的那样。一个问题是，我们需要螺旋的主干与画线的方向一致。我们可以通过旋转角度加 180 度(π)来沿所需角度定向螺旋。这就是我们在清单 [2-17](#Par57) 中所做的，它生成的三个不同的螺旋如图 [2-23](#Fig23) 所示。

![A432415_1_En_2_Fig23_HTML.jpg](A432415_1_En_2_Fig23_HTML.jpg)

图 2-23。

Output of the randomized spirals sketch .

```java
void setup() {
  size(600, 200);  
  frameRate(1);
}

void draw() {
  background(180);
  drawSpiral(100, 100, 0);
  drawSpiral(300, 100, QUARTER_PI);
  drawSpiral(500, 100, PI);
}

void drawSpiral(float posx, float posy, float angle) {
  pushMatrix();
  translate(posx, posy);  
  rotate(angle + PI);
  noFill();
  beginShape();
  float maxt = random(5, 20);
  float maxr = random(50, 80);
  float x0 = maxr * cos(1);
  float y0 = maxr * sin(1);
  for (float t = 1; t < maxt; t += 0.1) {
    float r = maxr/t;
    float x = r  * cos(t) - x0;
    float y = r  * sin(t) - y0;
    vertex(x, y);
  }
  endShape();
  popMatrix();
}

Listing 2-17.Adding Random Variability to Our Spiral-Generation Algorithm

```

我们现在可以将所有这些元素放在一个简单的绘图应用程序中，将树叶、藤蔓和水果添加到手绘线条中(清单 [2-18](#Par59) )。当按下鼠标/触摸屏时，会随机添加树叶和藤蔓。我们可以通过设置在每一帧中绘制新叶子或藤蔓的概率来控制细节的数量。通过使用值 0.05，平均来说，我们将每 20 帧添加一个有按压事件的新元素。通过从当前和先前鼠标/触摸位置之间的差异构建一个`PVector`对象来计算螺旋藤蔓与最后一条线段的连接角度。`PVector`是一个处理 2D 和 3D 向量的内置类。这个类包含几个实用函数，其中一个给我们矢量的航向角；即向量沿 x 轴的角度。图 [2-24](#Fig24) 显示了用该 app 绘制的图纸。

![A432415_1_En_2_Fig24_HTML.jpg](A432415_1_En_2_Fig24_HTML.jpg)

图 2-24。

Output of the vine-drawing sketch

```java
void setup() {
  fullScreen();  
  noFill();
  colorMode(HSB, 360, 99, 99);
  strokeWeight(2);
  stroke(210);  
  background(0, 0, 99);
}

void draw() {
  if (mousePressed) {
    line(pmouseX, pmouseY, mouseX, mouseY);
    if (random(1) < 0.05) {    
      PVector dir = new PVector(mouseX - pmouseX, mouseY - pmouseY);
      float a = dir.heading();
      drawSpiral(mouseX, mouseY, a);
    }
    if (random(1) < 0.05) {
      drawFlower(mouseX, mouseY);
    }
  }
}

void keyPressed() {
  background(0, 0, 99);
}

void drawFlower(float xc, float yc) {
  pushMatrix();
  pushStyle();
  noStroke();
  translate(xc, yc);  
  fill(random(60, 79), random(50, 60), 85, 190);
  beginShape();
  int numLobes = int(random(4, 10));
  for (int i = 0; i <= numLobes; i++) {
     float a = map(i, 0, numLobes, 0, TWO_PI);
     float a1 = map(i + 1, 0, numLobes, 0, TWO_PI);
     float r = random(10, 50);
     float x = r * cos(a);
     float y = r * sin(a);
     float x1 = r * cos(a1);
     float y1 = r * sin(a1);    
     vertex(0, 0);     
     vertex(0, 0);
     bezierVertex(x, y, x1, y1, 0, 0);     
  }
  endShape();
  popStyle();
  popMatrix();
}

void drawSpiral(float xc, float yc, float a) {
  pushMatrix();
  pushStyle();
  translate(xc, yc);  
  rotate(PI + a);
  noFill();
  beginShape();
  float maxt = random(5, 10);
  float maxr = random(20, 70);
  float sign = (random(1) < 0.5) ? -1 : +1;  
  float x0 = maxr * cos(sign);
  float y0 = maxr * sin(sign);
  for (float t = 1; t < maxt; t += 0.5) {
    float r = maxr/t;
    float x = r  * cos(sign * t) - x0;
    float y = r  * sin(sign * t) - y0;
    vertex(x, y);
  }
  endShape();
  noStroke();
  fill(random(310, 360), 80, 80);
  float x1 = (maxr/maxt) * cos(sign * maxt) - x0;
  float y1 = (maxr/maxt) * sin(sign * maxt) - y0;
  float r = random(5, 10);
  ellipse(x1, y1, r, r);
  popStyle();
  popMatrix();
}

Listing 2-18.Full Vine-Drawing Sketch

```

## 摘要

我们现在已经对处理语言有了一个总体的了解，并且能够使用它的一些函数和变量来绘制形状、设置颜色、应用变换以及通过鼠标或触摸屏处理用户交互。尽管我们只涉及了处理中所有可用功能的一小部分，但我们在这里看到的应该给我们足够的材料来探索算法绘图，制作我们自己的交互式草图，并作为 Android 应用程序运行它们。